#
# $Id: QS_ClientEventLimitCount.htt,v 1.4 2013-08-22 19:12:58 pbuchbinder Exp $
#
#  QS_ClientEventLimitCount 3 10
#  QS_ClientIpFromHeader      Y-Forwarded-For
#
#  QS_SetEnvResHeaderMatch    X-Login           failed
#  QS_SetEnvIf                X-Login    !QSNOT QS_Limit=yes
#  QS_UnsetResHeader          X-Login
#

INCLUDE scripts/ports

CLIENT

_LOOP 2
_REQ localhost $QS_PORT_BASE
__GET /htt/index1.html HTTP/1.1
__Host: localhost
__Connection: keep-alive
__Y-Forwarded-For: 10.10.10.1
__
_EXPECT . "!X-Login"
_EXPECT . "200"
_EXPECT . "AS1"
_WAIT
_CLOSE
_END LOOP

_LOOP 3
_REQ localhost $QS_PORT_BASE
__GET /htt/index1.html HTTP/1.1
__Host: localhost
__Connection: keep-alive
__Y-Forwarded-For: 10.10.10.2
__
_EXPECT . "!X-Login"
_EXPECT . "200"
_EXPECT . "AS2"
_WAIT
_CLOSE
_END LOOP
_SLEEP 200

_REQ localhost $QS_PORT_BASE
__GET /htt/index1.html HTTP/1.1
__Host: localhost
__Connection: keep-alive
__Y-Forwarded-For: 10.10.10.2
__
_EXPECT . "code=067"
_WAIT
_CLOSE
_SLEEP 200

_EXPECT EXEC "mod_qos\(067\): access denied, QS_ClientEventLimitCount rule: event=QS_Limit"
_EXEC tail -1 logs/error_log

_SET MSG_MISSINGHEADER=mod_qos\(069\): no valid IP header found .@hp.: header 'Y-Forwarded-For' not available, fallback to connection's IP 127.0.0.1
_SET MSG_MISSINGHEADER2=mod_qos\(069\): no valid IP header found .@hp.: invalid header value '10.10.10.5, 10.10.10.6', fallback to connection's IP 127.0.0.1
_EXPECT EXEC "!${MSG_MISSINGHEADER}"
_EXEC tail -3 logs/error_log
_EXPECT EXEC "!${MSG_MISSINGHEADER2}"
_EXEC tail -3 logs/error_log

_REQ localhost $QS_PORT_BASE
__GET /htt/index1.html HTTP/1.1
__Host: localhost
__Connection: keep-alive
__Y-Forwarded-For: 10.10.10.3
__
_EXPECT . "!X-Login"
_EXPECT . "200"
_EXPECT . "AS3"
_WAIT
_CLOSE

_SLEEP 12000
_REQ localhost $QS_PORT_BASE
__GET /htt/index1.html HTTP/1.1
__Host: localhost
__Connection: keep-alive
__Y-Forwarded-For: 10.10.10.2
__
_EXPECT . "!X-Login"
_EXPECT . "200"
_EXPECT . "AS2"
_WAIT
_CLOSE

# missing header
_REQ localhost $QS_PORT_BASE
__GET /htt/index1.html HTTP/1.1
__Host: localhost
__Connection: keep-alive
__
_EXPECT . "!X-Login"
_EXPECT . "200"
_EXPECT . "AS4"
_WAIT
_CLOSE
_SLEEP 200
_EXPECT EXEC "${MSG_MISSINGHEADER}"
_EXEC tail -1 logs/error_log

# invalid header
_REQ localhost $QS_PORT_BASE
__GET /htt/index1.html HTTP/1.1
__Host: localhost
__Connection: keep-alive
__Y-Forwarded-For: 10.10.10.5, 10.10.10.6
__
_EXPECT . "!X-Login"
_EXPECT . "200"
_EXPECT . "AS5"
_WAIT
_CLOSE
_SLEEP 200
_EXPECT EXEC "${MSG_MISSINGHEADER2}"
_EXEC tail -1 logs/error_log

END

SERVER $QS_PORT_BASE6

_LOOP 2
_RES
_WAIT
__HTTP/1.1 200 OK
__Content-Length: AUTO 
__Content-Type: text/html
__X-Login: failed
__
__==AS1==
_CLOSE
_END LOOP

_LOOP 3
_RES
_WAIT
__HTTP/1.1 200 OK
__Content-Length: AUTO 
__Content-Type: text/html
__X-Login: failed
__
__==AS2==
_CLOSE
_END LOOP

_RES
_WAIT
__HTTP/1.1 200 OK
__Content-Length: AUTO 
__Content-Type: text/html
__X-Login: failed
__
__==AS3==
_CLOSE

_RES
_WAIT
__HTTP/1.1 200 OK
__Content-Length: AUTO 
__Content-Type: text/html
__X-Login: failed
__
__==AS2==
_CLOSE

_RES
_WAIT
__HTTP/1.1 200 OK
__Content-Length: AUTO 
__Content-Type: text/html
__X-Login: failed
__
__==AS4==
_CLOSE

_RES
_WAIT
__HTTP/1.1 200 OK
__Content-Length: AUTO 
__Content-Type: text/html
__X-Login: failed
__
__==AS5==
_CLOSE

END
