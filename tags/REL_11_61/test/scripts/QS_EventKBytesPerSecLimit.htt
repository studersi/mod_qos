#
# $Id$
#
#   BrowserMatch QS_EventKBytesPerSecLimit  eventkbytespersec
#   QS_EventKBytesPerSecLimit  eventkbytespersec 202
#

INCLUDE scripts/ports

CLIENT

_SET bytes=0
_DATE:GET_TIME start

_BPS 2000 23
# 29427 bytes each request
# ~20req/sec
# ~640kb/sec 
_REQ server1 $QS_PORT_BASE1
__GET /aaa/index2.html HTTP/1.1
__Host: server1
__User-Agent: QS_EventKBytesPerSecLimit
__Connection: keep-alive
__
_MATCH headers "Content-Length: ([0-9]*)" ct
_MATCH headers "Connection: (.*)" CONNECTION
_EXPECT . "Revision"
_WAIT
_MATH:OP $bytes ADD $ct bytes

_IF "$CONNECTION" MATCH "close"
_CLOSE
_END IF

_END BPS
_DATE:GET_TIME end
_MATH:OP $end SUB $start duration
_MATH:OP $bytes DIV $duration bms
_DEBUG "kbytes/sec (netto) $bms"
_IF $bms GT 250
_EXIT FAILED
_END IF
_IF $bms LT 150
_EXIT FAILED
_END IF

_CLOSE
_REQ localhost $QS_PORT_BASE
__GET /qos HTTP/1.1
__Host: localhost
__Connection: keep-alive
__
_EXPECT . "200"
# <<tr class="rows"><!--3--><td>var={eventkbytespersec}</a></td><td>-</td><td>-</td><td>-</td><td>-</td><td>-</td><td style="background-color: rgb(240,153,155);">63&nbsp;ms</td><td>202</td><td >80</td></tr>
_EXPECT . "<!--3--><td>var=.eventkbytespersec.</a></td><td>-</td><td>-</td><td>-</td><td>-</td><td>-</td><td style=.background-color: rgb....,...,....;.>[0-9]{2,3}&nbsp;ms</td><td>202</td>"
_WAIT
_CLOSE

END
